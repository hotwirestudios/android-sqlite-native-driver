apply plugin: 'com.android.library'

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.1.3'
    }
}

android {
    repositories {
        mavenCentral()
        jcenter()
    }

    compileSdkVersion 24
    buildToolsVersion '25.0.0'

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 24
        versionCode 1
        versionName "1.0"
        consumerProguardFiles 'proguard-rules.txt'
    }

    sourceSets {
        main {
            jniLibs.srcDirs += 'libs'
        }
    }

    // See https://github.com/bytedeco/javacpp/blob/master/src/main/resources/org/bytedeco/javacpp/properties/ for defaults
    libraryVariants.all { variant ->
        variant.javaCompile.doLast {
            println 'javacpp ' + variant.name
            def projectRoot = projectDir.getCanonicalPath()
            def ndkRoot = android.getNdkDirectory().getCanonicalPath()
            def cp = variant.javaCompile.destinationDir.getCanonicalPath()

            buildSQLiteTask(cp, ndkRoot, projectRoot, 'arm', 'arm-linux-androideabi', 'arm-linux-androideabi', '-march=armv5te -mtune=xscale -msoft-float', 'armeabi')
            buildSQLiteTask(cp, ndkRoot, projectRoot, 'arm', 'arm-linux-androideabi', 'arm-linux-androideabi', '-march=armv7-a -mfloat-abi=softfp -mfpu=neon -ffast-math -Wl,--fix-cortex-a8', 'armeabi-v7a')
            buildSQLiteTask(cp, ndkRoot, projectRoot, 'arm64', 'aarch64-linux-android', 'aarch64-linux-android', ' ', 'arm64-v8a')
            buildSQLiteTask(cp, ndkRoot, projectRoot, 'x86', 'x86', 'i686-linux-android', ' ', 'x86')
            buildSQLiteTask(cp, ndkRoot, projectRoot, 'x86_64', 'x86_64', 'x86_64-linux-android', ' ', 'x86_64')
            println 'javacpp done'
        }
    }
}

configurations {
    javacpp
}

dependencies {
    compile 'org.bytedeco:javacpp:1.2.1'
    javacpp 'org.bytedeco:javacpp:1.2.1'
    compile 'com.android.support:support-annotations:24.2.0'
    compile 'com.parse.bolts:bolts-tasks:1.4.0'
}

def buildSQLiteTask(cp, ndkRoot, projectRoot, androidArchitecture, compilerArchitecture, compilerPrefix, flags, outputArchitecture) {
    javaexec {
        main 'org.bytedeco.javacpp.tools.Builder'
        classpath configurations.javacpp
        args '-cp', cp,
                '-properties', 'android-arm',
                '-Dplatform.root=' + ndkRoot,
                '-Dplatform.sysroot=platforms/android-23/arch-' + androidArchitecture,
                '-Dplatform.compiler.default=' + flags,
                '-Dplatform.compiler=toolchains/' + compilerArchitecture + '-4.9/prebuilt/darwin-x86_64/bin/' + compilerPrefix + '-g++',
                '-Dplatform.includepath=sources/cxx-stl/gnu-libstdc++/4.9/include:sources/cxx-stl/gnu-libstdc++/4.9/libs/' + outputArchitecture + '/include:' + projectRoot + '/sqlite-amalgamation:' + projectRoot + '/rapidjson/include:' + projectRoot + '/sqlite-rapidjson',
                '-Dplatform.linkpath=sources/cxx-stl/gnu-libstdc++/4.9/libs/' + outputArchitecture + ':' + projectRoot + '/libs/' + outputArchitecture,
                '-d', projectRoot + '/libs/' + outputArchitecture,
                'com.hotwirestudios.sqlite.driver.SQLiteNative'
    }
}
